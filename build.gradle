apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

sourceCompatibility = 7

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = "6.7"
}

findbugs {
  ignoreFailures = true
  sourceSets = [sourceSets.main]
  effort = "max"
}

pmd {
  ignoreFailures = true
  sourceSets = [sourceSets.main]
}

test {
  reports.junitXml.destination = file('build/reports/tests')
}

sourceSets {
  main {
        java {
            srcDir 'cases/src'
            srcDir 'backend/src'
        }
  }
  test {
      java {
          srcDir 'tests/test'
      }
      resources{
          srcDir 'tests/resources'
      }
  }

  cli {
      java {
          srcDir 'util/src'
      }
  }
}

dependencies {
      compile project(':javarosa')

      cliCompile project(path: ':javarosa', configuration: 'harness')
      cliCompile sourceSets.main.output

      testCompile sourceSets.main.output
      testCompile sourceSets.cli.output
      testCompile group: 'junit', name: 'junit', version: '4.+'
}

jar {
    baseName = "commcare-libraries"
}

jar.dependsOn test

configurations {
  cliOutput.extendsFrom (cliCompile)
}

task cliJar(type: Jar, dependsOn: cliClasses) {
    baseName = "commcare-cli"
    from sourceSets.cli.output
    from {
        configurations.cliCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.cliRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.commcare.util.Harness'
    }
    
}

artifacts {
  cliOutput cliJar
}

task copyTestResources(type: Copy) {
    from sourceSets.test.resources
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task jenkinsTest {
    inputs.files test.outputs.files
    doLast {
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile { it.lastModified = timestamp }
    }
}

check.dependsOn(jenkinsTest)
