buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id "me.champeau.jmh" version "0.6.5"
}

// Needed even though it looks unused
sourceCompatibility = 1.8

configurations {
    ccapi.extendsFrom(ccapiImplementation)
    cliOutput.extendsFrom(cliImplementation)
    harness
    translate.extendsFrom(translateImplementation)
    testsAsJar
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

test {
    reports.junitXml.destination = file('build/reports/tests')
}

sourceSets {
    translate {
        java {
            compileClasspath += main.output
        }
    }

    cli {
        java {
            compileClasspath += main.output
            compileClasspath += translate.output
        }
    }

    ccapi {
        java {
            compileClasspath += main.output
            srcDir 'src/cli/java'
            exclude 'org/commcare/util/cli/**'
            exclude 'org/commcare/util/mocks/**'
        }
    }

    test {
        java {
            compileClasspath += cli.output
            compileClasspath += ccapi.output
            compileClasspath += translate.output
        }
    }


}

dependencies {
    compile 'com.github.stefanhaustein:kxml2:2.4.1'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    // Added so Android Studio recognizes libs in util jar projects
    compile 'org.json:json:20140107'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'xpp3:xpp3:1.1.4c'
    compile 'joda-time:joda-time:2.9.4'
    compile 'com.carrotsearch:hppc:0.7.2'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.12.12'
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    compile 'io.reactivex.rxjava2:rxjava:2.1.1'

    cliCompile 'org.json:json:20140107'
    cliCompile 'com.github.stefanhaustein:kxml2:2.4.1'
    cliCompile 'commons-cli:commons-cli:1.3.1'
    cliCompile 'xpp3:xpp3:1.1.4c'
    cliCompile 'joda-time:joda-time:2.9.4'
    cliCompile 'io.reactivex.rxjava2:rxjava:2.1.1'
    cliCompile 'com.squareup.retrofit2:retrofit:2.3.0'
    cliCompile 'com.squareup.okhttp3:okhttp:4.4.1'

    testCompile sourceSets.main.output
    testCompile sourceSets.api.output
    testCompile sourceSets.cli.output
    testCompile sourceSets.translate.output
    testCompile 'junit:junit:4.12'
    testCompile 'org.json:json:20140107'
    testCompile 'org.xerial:sqlite-jdbc:3.28.0'
    testCompile 'joda-time:joda-time:2.9.4'
    testCompile 'com.squareup.retrofit2:retrofit:2.3.0'
    testCompile 'com.squareup.okhttp3:okhttp:4.4.1'

    apiCompile sourceSets.main.output
    apiCompile 'org.json:json:20140107'
    apiCompile 'org.xerial:sqlite-jdbc:3.28.0'
    apiCompile 'com.github.stefanhaustein:kxml2:2.4.1'
    apiCompile 'joda-time:joda-time:2.9.4'
    apiCompile 'com.carrotsearch:hppc:0.7.2'
    apiCompile 'io.reactivex.rxjava2:rxjava:2.1.1'
    apiCompile "com.squareup.retrofit2:retrofit:2.3.0"
    apiCompile 'com.squareup.okhttp3:okhttp:4.4.1'

    translateCompile 'com.github.stefanhaustein:kxml2:2.4.1'
    translateCompile 'xpp3:xpp3:1.1.4c'
    translateCompile 'commons-cli:commons-cli:1.3.1'
    translateCompile files('lib/json-simple-1.1.1.jar')
    translateCompile 'joda-time:joda-time:2.9.4'
    translateCompile 'org.json:json:20140107'
    translateCompile 'io.reactivex.rxjava2:rxjava:2.1.1'
    translateCompile "com.squareup.retrofit2:retrofit:2.3.0"
    translateCompile 'com.squareup.okhttp3:okhttp:4.4.1'

    // 'compile' dependency is also needed to run 'gradle jmh' for some reason,
    // but leaving it in conflicts with apk builds, so only uncomment the
    // following lines when jmh needed:
    /*
            compile 'org.openjdk.jmh:jmh-core:1.17.3'
            compile 'org.openjdk.jmh:jmh-generator-annprocess:1.17.3'
            jmhCompile 'org.openjdk.jmh:jmh-core:1.17.3'
            jmhCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.17.3'
            */
}

jar {
    baseName = "commcare-libraries"
}

jar.dependsOn test

task cliJar(type: Jar, dependsOn: cliClasses) {
    baseName = "commcare-cli"
    from sourceSets.translate.output
    from sourceSets.cli.output
    from sourceSets.main.output
    from {
        configurations.cliOutput.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.commcare.util.cli.CliMain'
    }
    duplicatesStrategy = DuplicatesStrategy.FAIL
    filesMatching(["**/LICENSE.txt","**/NOTICE.txt"]) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

task ccapiJar(type: Jar, dependsOn: ccapiClasses) {
    baseName = "commcare-api"
    from sourceSets.ccapi.output
    from sourceSets.main.output
    from {
        configurations.ccapi.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = DuplicatesStrategy.FAIL
    filesMatching(["**/LICENSE.txt","**/NOTICE.txt"]) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

task harnessJar(type: Jar, dependsOn: translateClasses) {
    baseName = "javarosa-cli"
    from sourceSets.translate.output
    from sourceSets.main.output
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.engine.Harness'
    }
    duplicatesStrategy = DuplicatesStrategy.FAIL
    filesMatching(["**/LICENSE.txt","**/NOTICE.txt"]) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

// used to provide the test source files to external projects, such as commcare-android,
// which might want to import classes
task testsrcJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from files(sourceSets.test.output)
    from files(sourceSets.cli.output)
    duplicatesStrategy = DuplicatesStrategy.FAIL
}


task formTranslateJar(type: Jar, dependsOn: translateClasses) {
    baseName = "form_translate"
    from sourceSets.translate.output
    from sourceSets.main.output
    from {
        configurations.translate.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.xform.schema.Harness'
    }
    duplicatesStrategy = DuplicatesStrategy.FAIL
    filesMatching(["**/LICENSE.txt","**/NOTICE.txt", "**/org.xmlpull.v1.XmlPullParserFactory","org/xmlpull/v1/*"]) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

artifacts {
    cliOutput cliJar
    ccapi ccapiJar
    harness harnessJar
    translate formTranslateJar
    testsAsJar testsrcJar
}

task copyTestResources(type: Copy) {
    from sourceSets.test.resources
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

def buildDeps = hasProperty('javarosaDeps') ? javarosaDeps : null

/* for use primarily by the build server to extract the required 3rd-party
libraries into the javarosa source tree. to use this in your own build, set the
'extract-libs-fresh' property, then also set the 'javarosa-deps' property to
the path of the library archive (make sure you are using the version of the
archive compatible with the version you wish to build) */
task extractLibs(type: Copy) {
    if (buildDeps != null) {
        from zipTree(file("${buildDeps}")) into file("${projectDir}")
    }
}

compileJava.dependsOn extractLibs


task jenkinsTest {
    inputs.files test.outputs.files
    doLast {
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile { it.lastModified = timestamp }
    }
}

check.dependsOn(jenkinsTest)

jmh {
    duplicateClassesStrategy = DuplicatesStrategy.FAIL
    fork = 2
    iterations = 10
    warmupIterations = 20
    includeTests = true
    // default mode is 'throughput'; where a larger score is better
}
