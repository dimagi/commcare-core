apply plugin: 'java'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

test {
  reports.junitXml.destination = file('build/reports/tests')
}

sourceSets {
    // javarosa configs
    javarosa {
          java {
              srcDir 'javarosa/src/main/java'
          }
    }
    harness {
        java {
            srcDir 'javarosa/util/schema-gen/src'
            srcDir 'javarosa/src/main/java'
        }
        resources{
            srcDir 'javarosa/src/test/resources'
        }
    }

    translate {
        java {
            srcDir 'javarosa/util/schema-gen/src'
            srcDir 'javarosa/util/validator/org.javarosa.xform.validator/src'
            srcDir 'javarosa/src/main/java'
            compileClasspath += main.output
        }
    }

    // commcare configs
  main {
        java {
            srcDir 'javarosa/src/main/java'
            srcDir 'cases/src'
            srcDir 'backend/src'
            srcDir 'core'
            srcDir 'modern'
        }
        resources {
            srcDir 'cases/resources'
        }
  }

  cli {
      java {
          srcDir 'util/src'
          srcDir 'javarosa/util/schema-gen/src'
          srcDir 'javarosa/src/main/java'
          compileClasspath += main.output
      }
  }
    api {
        java {
            srcDir 'api'
            srcDir 'util/src/org/commcare/util/screen'
            compileClasspath += main.output
        }
    }

    test {
        java {
            srcDir 'util/src'
            srcDir 'tests/test'
            srcDir 'javarosa/src/test/java'
            compileClasspath += api.output
        }
        resources{
            srcDir 'tests/resources'
            srcDir 'javarosa/src/test/resources'
        }
    }

}

dependencies {
      compile 'kxml2:kxml2:2.3.0'
      compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1'
      compile files('javarosa/lib/regexp-me.jar')
      // Added so Android Studio recognizes libs in util jar projects
      compile 'org.json:json:20140107'
      compile 'commons-cli:commons-cli:1.3.1'
      compile 'xpp3:xpp3:1.1.4c'
      compile 'joda-time:joda-time:2.9.3'

      cliCompile 'org.json:json:20140107'
      cliCompile files('javarosa/lib/regexp-me.jar')
      cliCompile 'kxml2:kxml2:2.3.0'
      cliCompile 'commons-cli:commons-cli:1.3.1'
      cliCompile 'xpp3:xpp3:1.1.4c'
      cliCompile 'joda-time:joda-time:2.9.3'

      testCompile sourceSets.main.output
      testCompile sourceSets.api.output
      testCompile sourceSets.cli.output
      testCompile 'junit:junit:4.12'
      testCompile 'org.json:json:20140107'
      testCompile 'org.xerial:sqlite-jdbc:3.8.11.1'
      testCompile 'joda-time:joda-time:2.9.3'

      apiCompile sourceSets.main.output
      apiCompile 'org.json:json:20140107'
      apiCompile 'org.xerial:sqlite-jdbc:3.8.11.1'
      apiCompile 'kxml2:kxml2:2.3.0'
      apiCompile files('javarosa/lib/regexp-me.jar')
      apiCompile 'joda-time:joda-time:2.9.3'

      harnessCompile 'kxml2:kxml2:2.3.0'
      harnessCompile 'xpp3:xpp3:1.1.4c'
      harnessCompile 'commons-cli:commons-cli:1.3.1'
      harnessCompile files('javarosa/lib/regexp-me.jar')
      harnessCompile 'org.json:json:20140107'
      harnessCompile 'joda-time:joda-time:2.9.3'

      translateCompile 'kxml2:kxml2:2.3.0'
      translateCompile 'xpp3:xpp3:1.1.4c'
      translateCompile 'commons-cli:commons-cli:1.3.1'
      translateCompile files('javarosa/lib/regexp-me.jar')
      translateCompile files('javarosa/util/schema-gen/lib/json-simple-1.1.1.jar')
      translateCompile 'joda-time:joda-time:2.9.3'
      translateCompile 'org.json:json:20140107'
}

jar {
    baseName = "commcare-libraries"
}

jar.dependsOn test

configurations {
  api
  cliOutput.extendsFrom (cliCompile)
  harness
  translate
  testsAsJar
}

task cliJar(type: Jar, dependsOn: cliClasses) {
    baseName = "commcare-cli"
    from sourceSets.cli.output
    from sourceSets.main.output
    from {
        configurations.cliCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.cliRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.commcare.util.cli.CliMain'
    }
    
}

task apiJar(type: Jar, dependsOn: apiClasses) {
    baseName = "commcare-api"
    from sourceSets.api.output
    from sourceSets.main.output
    from sourceSets.cli.output
    from {
        configurations.apiCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.apiRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task harnessJar(type: Jar, dependsOn: harnessClasses) {
    baseName = "javarosa-cli"
    from sourceSets.harness.output
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.engine.Harness'
    }
}

// used to provide the test source files to external projects, such as odk,
// which might want to import classes
task testsrcJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from files(sourceSets.test.output.classesDir)
}


task formTranslateJar(type: Jar, dependsOn: translateClasses) {
    baseName = "form_translate"
    from sourceSets.translate.output
    from sourceSets.main.output
    from {
        configurations.translateCompile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.translateRuntime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Main-Class': 'org.javarosa.xform.schema.Harness'
    }
}

artifacts {
  cliOutput cliJar
  api apiJar
  harness harnessJar
  translate formTranslateJar
  testsAsJar testsrcJar
}

task copyTestResources(type: Copy) {
    from sourceSets.test.resources
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

def buildDeps = hasProperty('javarosaDeps') ? javarosaDeps : null

/* for use primarily by the build server to extract the required 3rd-party
libraries into the javarosa source tree. to use this in your own build, set the
'extract-libs-fresh' property, then also set the 'javarosa-deps' property to
the path of the library archive (make sure you are using the version of the
archive compatible with the version you wish to build) */
task extractLibs(type: Copy) {
    if (buildDeps != null) {
        from zipTree(file("${buildDeps}")) into file("${projectDir}")
    }
}

compileJava.dependsOn extractLibs


task jenkinsTest {
    inputs.files test.outputs.files
    doLast {
        def timestamp = System.currentTimeMillis()
        test.getReports().getJunitXml().getDestination().eachFile { it.lastModified = timestamp }
    }
}

check.dependsOn(jenkinsTest)
